/**
 * Module:	inst_rootpart.ycp
 *
 * Authors:	Stefan Schubert <schubi@suse.de>
 *		Arvin Schnell <arvin@suse.de>
 *
 * Purpose:	Select root partition for update or booting.
 *		Update::validRootPartitions must be filled before
 *		calling this module.
 */
{
    textdomain "update";

    import "Wizard";
    import "Update";


    if ( !Mode::initial )
    {
	// Update has been started from installed system.
	return `auto;
    }


    // This dialog comes in three different flavors: `update_dialog,
    // `update_popup and `boot_popup
    symbol flavor = WFM::Args (2);


    list partition_list = [];
    foreach (`p, `r, Update::validRootPartitions, ``{
	partition_list = add (partition_list, `item (`id (p), r, p));
    });


    string heading = "";
    if (flavor == `boot_popup)
	heading = _("Select System to Boot");
    else
	heading = _("Select System to Update");


    term contents = `VBox (
			   // heading for 'select root partiton' dialogue
			   `Heading ( heading ),
			   `VSpacing (0.5),
			   `HCenter (
				     `HSquash (
					       `VBox (
						      `HSpacing( 40 ),	// force minimum width
						      // label for selection list of root partitions
						      `Left (`Label (_("Select System:"))),
						      `Table (
							      `id (`partition),
							      `header (_("System"), _("Partition")),
							      partition_list
							      )
						      )
					       )
				     ),
			   `VSpacing (2)
			   );

    // helptext for root partition
    string help_text = _("<p>
Select the <b>root partition</b> to use.
</p>
");

    if (flavor == `update_dialog)
    {
	Wizard::OpenAcceptDialog ();
	Wizard::SetContents ("", contents, help_text, Args(0), Args(1));
    }
    else
    {
	term buttons = `PushButton (`id(`next), `opt(`default), UI::OKButtonLabel());

	if (flavor == `boot_popup)
	{
	    buttons = `HBox (
			     `HWeight( 1, `PushButton(`id(`next), `opt(`default), UI::OKButtonLabel() ) ),
			     `HSpacing( 1 ),
			     `HWeight( 1, `PushButton(`id(`cancel), UI::CancelButtonLabel() ) )
			     );
	}

	term full = `HBox (
			   `VSpacing( 16 ),		// force dialog height
			   `VBox(
				 `HSpacing( 30 ),	// force help text width
				 `RichText( help_text )
				 ),
			   `HSpacing( 3 ),
			   `VBox(
				 `VSpacing( 1 ),
				 contents,
				 buttons
				 ),
			   `HSpacing( 3 )
			   );

	UI::OpenDialog (full);
    }

    if (flavor == `update_dialog || flavor == `update_popup)
    {
	if (size (Update::selectedRootPartition) > 0)
	    UI::ChangeWidget (`id(`partition), `CurrentItem, Update::selectedRootPartition);
    }
    else
    {
	if (size (Update::selectedRootPartitionForBooting) > 0)
	    UI::ChangeWidget (`id(`partition), `CurrentItem, Update::selectedRootPartitionForBooting);
    }

    any ret = nil;

    while (true)
    {
	if (flavor == `update_dialog)
	    ret = Wizard::UserInput ();
	else
	    ret = UI::UserInput ();

	if (ret == `abort && CallFunction (`inst_confirm_abort (`painless)))
	    break;

	if (ret == `cancel || ret == `back || ret == `next)
	    break;
    }

    if (ret == `next)
    {
	if (flavor == `update_dialog || flavor == `update_popup)
	    Update::selectedRootPartition = UI::QueryWidget (`id(`partition), `CurrentItem);
	else
	    Update::selectedRootPartitionForBooting = UI::QueryWidget (`id(`partition), `CurrentItem);
    }

    if (flavor == `update_dialog)
	Wizard::CloseDialog ();
    else
	UI::CloseDialog ();

    return ret;
}
