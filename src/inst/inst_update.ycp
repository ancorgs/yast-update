/**
 * Module: 		inst_update.ycp
 *
 * Authors:		Stefan Schubert <schubi@suse.de>
 *			Arvin Schnell <arvin@suse.de>
 *
 * Purpose:
 * Displays software selection screen of previous installed software-groups.
 * Show checkboxes for software categories. Let the user select his software.
 * if he want to UPGRADE his system.
 */

{
    textdomain "update";

    import "Mode";
    import "Installation";
    import "Language";
    import "Wizard";
    import "Popup";
    import "RootPart";
    import "Update";
    import "Packages";


    // screen title for update options
    string title = _("Update Options");


    // current selection and list of base selections
    string current_selection = Update::GetSelection ();
    list available_base_selections = Pkg::GetSelections (`available, "base");


    // create box with base selections
    term baseconfsbox = `VBox ();
    foreach (`selection, available_base_selections, ``{
	map selection_data = Pkg::SelectionData (selection);
	string selection_summary = selection_data["summary"]:"";
	baseconfsbox = add (baseconfsbox, `Left(`RadioButton(`id(selection),
							     `opt(`notify, `autoShortcut),
							     selection_summary,
							     selection == current_selection)));
    });

    // Checking: already selected addons or single selection?

    string wrn_msg = "";

    if (Update::userChangedState)		// check if state changed
    {
	// Display warning message
	wrn_msg = _("\
You have already chosen software from \"Detailed selection\".\n\
You will lose that selection if you change the basic selection.");
    }

    // Build and show dialog

    string from_version = RootPart::GetInfoOfSelected (`name);
    string to_version = Update::updateVersion;

    term contents = `HVSquash(
	`VBox(
	      // label showing from what version to what version we are updating
	      `Left (`Label (sformat (_("Update from %1 to %2"), from_version, to_version))),
	      `VSpacing (1),
	      // frame title for update selection
	      `Frame( _("Update Mode"),
		      `VBox(
			    `VSpacing(1),
			    `RadioButtonGroup( `id(`bgoup), `opt(`notify),
					       `VBox(
						     `Left(`RadioButton(`id(`upgrade),`opt(`notify),
									// radio button label for update including new packages
									_("&Update with installation of new software and features
based on the selection:"), false)),
						     `VSpacing(0.5),
						     `HBox(
							   `HSpacing(4),
							   `RadioButtonGroup(`id(`baseconf), baseconfsbox)
							   ),
						     `VSpacing(1.5),
						     `Left(`RadioButton(`id(`notupgrade),`opt(`notify),
									// radio button label for update of already installed packages only
									_("Only u&pdate installed packages."), true)),
						     `VSpacing(1.0)
						     )
					       )
			    )
		      ),
	      `VSpacing (1),
	      `Left(`CheckBox(`id(`delete),
			      // check box label
			      // translator: add a & shortcut
			      _("&Delete unmaintained packages."), true)),
	      `VSpacing(1),
	      `Label( `id(`wrn_label), wrn_msg )
	      )
	);


    // help text for dialog "update options" 1/4
    string helptext = _("<p>The update options differs between two modes. In
either case it is recommended to make a backup of your personal data.</p>");

    // help text for dialog "update options" 2/4
    helptext = helptext + _("<p><b>With new software:</b> This default setting
will update the existing software and install all new features and benefits of
the new SuSE Linux version. The selection is based on the former pre-defined
software selection.</p>");

    // help text for dialog "update options" 3/4
    helptext = helptext + _("<p><b>Only installed packages:</b> This selection
will only update the packages already installed on your system. <i>Note:</i>
New software in the pre-defined software selection, e.g. new YaST modules, is
not available after the update. You might miss advertised features.</p>");

    // help text for dialog "update options" 4/4
    helptext = helptext + _("<p>After the update some software might not
function anymore. Active <b>Delete unmaintained packages</b> to delete those
packages during the update.</p>");


    Wizard::OpenAcceptDialog ();

    Wizard::SetContents (title, contents, helptext, Args(0), Args(1));

    UI::ChangeWidget(`id(`upgrade), `Value, !Update::onlyUpdateSelected);
    UI::ChangeWidget(`id(`notupgrade), `Value, Update::onlyUpdateSelected);

    foreach (`sel, available_base_selections, ``{
	UI::ChangeWidget(`id(sel), `Enabled, !Update::onlyUpdateSelected);
    });

    UI::ChangeWidget(`id(`delete), `Value, Update::deleteOldPackages);

    any ret = nil;

    Update::userChangedState = true;

    while (true)
    {
	ret = Wizard::UserInput ();

	if (ret == `abort && CallFunction (`inst_confirm_abort (`painless)))
	    break;

	if (ret == `upgrade || ret == `notupgrade)
	{
	    boolean tmp = UI::QueryWidget (`id(`upgrade), `Value);
	    foreach (`sel, available_base_selections, ``{
		UI::ChangeWidget(`id(sel), `Enabled, tmp);
	    });

	    continue;
	}

	if (ret == `next && Update::userChangedState)
	{
	    boolean something_changed = false;

	    if (UI::QueryWidget (`id(`notupgrade), `Value) != Update::onlyUpdateSelected)
		something_changed = true;

	    if (UI::QueryWidget (`id(`upgrade), `Value) &&
		current_selection != UI::QueryWidget(`id(`baseconf), `CurrentButton))
		something_changed = true;

	    if (UI::QueryWidget (`id(`delete), `Value ) != Update::deleteOldPackages)
		something_changed = true;

	    if (something_changed)
	    {
		// yes/no question
		if (!Popup::YesNo (_("Do you really want\nto reset your detailed selection?")))
		    continue;
	    }
	}

	if (ret == `back || ret == `next)
	    break;
    }

    if (ret == `next)
    {
	Update::onlyUpdateSelected = UI::QueryWidget (`id(`notupgrade), `Value);
	Update::deleteOldPackages = UI::QueryWidget (`id(`delete), `Value);

	Update::updateEvaluated = false;
	Update::userChangedState = false;

	if (Update::onlyUpdateSelected)
	    Pkg::SetSelection ("default");
	else
	    Pkg::SetSelection (UI::QueryWidget(`id(`baseconf), `CurrentButton));

	Pkg::ActivateSelections ();
    }

    Wizard::CloseDialog ();

    return ret;
}
